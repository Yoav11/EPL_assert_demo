using com.apama.exceptions.Exception;
using com.apamax.test.Assert;

event device {
	string id;
	string type;

	static action create(string id, string type) returns device {
		device temp := new device;

		temp.id := id;
		temp.type := type;
		return temp;
	}
}


monitor AssertTest {
	/** Initialize the application */
	action onload() {
		dictionary<string, any> options := {
			"isEnabled": true,
			"logLevel": "ERROR"
		};

		Assert asserter := Assert.createCustom(options);

		device device1 := device.create("abc", "sensor");
		device device2 := device.create("abcd", "sensor");

		asserter.assertEquals("stringsEqualTest", "hello world", "hello world");
		asserter.assertEquals("objectEqualsTest", device1, device2);

		asserter.assertNotEquals("floatNotEqualsTest", 3.56, 3.57);
		asserter.assertNotEquals("intNotEqualsTest", 1, 1);

		asserter.assertInRange("inRangeTest", 3, 2, 4);
		asserter.assertInRange("outOfRangeTest", 6, 1, 5);

		asserter.assertTrue("twoPlusTwoIsFour", (2+2)=4);
		asserter.assertTrue("twoPlusTwoIsFive", (2+2)=5);

		asserter.assertFalse("twoPlusTwoIsNotFive", (2+2)=5);
		asserter.assertFalse("twoPlusTwoIsNotFour", (2+2)=4);

		asserter.assertGreaterThan("greaterThanStringTest", "b", "a");
		asserter.assertGreaterThan("greaterThanIntTest", 1, 4);

		asserter.assertLesserThan("lesserThanStringTest", "Hello", "Helloo");
		asserter.assertLesserThan("lesserThanFloatTest", -0.19, -0.20);

		asserter.assertThrows("throwTest1", addTo1, [2]);
		asserter.assertThrows("throwTest2", addTo1, [1]);

		asserter.assertNoThrow("noThrowTest1", addTo1, [1]);
		asserter.assertNoThrow("noThrowTest2", addTo1, [2]);

		asserter.assertThrowsException("specificThrowTest1", addTo1, [2], "IllegalArgumentException");
		asserter.assertThrowsException("specificThrowTest2", addTo1, [2], "ArithmeticException");

		asserter.assertContains("containsTest1", device1, "id");
		asserter.assertContains("containsTest2", device1, "name");
	}

	action addTo1(integer i) returns integer {
		if(i != 1) {
			throw Exception("Argument is not 1", "IllegalArgumentException");
		} else {
			return i + 1;
		}
	}
}